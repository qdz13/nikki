#compdef nikki

_nikki_diaries() {
	local i datadir
	local -a dates files

	[ $# -eq 1 ] || return
	datadir="$1"
	[ -d "$datadir" ] || return

	dates=($(find "$datadir" -name backup -prune -o -type f -name '*.md' -print |
			 sed -e "s|^$datadir/||g" -e 's/\.md$//g' -e 's|/||g'))
	[ ${#dates[@]} -eq 0 ] && return

	i=$(tr ' ' '\n' <<<  "${dates[@]}")
	files+=("${dates[@]}")
	files+=($(sed -n "/^$(date '+%Y')/"'s/^.\{4\}//p' <<< "$i")) # Current year
	files+=($(sed -n "/^$(date '+%Y%m')/"'s/^.\{6\}//p' <<< "$i")) # Current month

	_values 'diary' $files
}

_nikki() {
	local datadir past
	local -a dates

	datadir="${XDG_DATA_HOME:-$HOME/.local/share}/nikki"

	_arguments \
		'1: :->cmd' \
		'*:: :->arg' && ret=0

	if [ "$state" = cmd ]; then
		_arguments \
			'(- *)'{-h,--help}'[Display help message and exit]' \
			'(-p --pager)'{-p,--pager}'[Set pager]:pager:(cat less bat glow)' && ret=0

		_values 'subcommand' \
			'edit[Edit diary]' \
			'ls[List diaries]' \
			'read[Read diary]' \
			'rm[Remove diary]' \
			'tree[List diaries in a tree-like format]' \
			'restore[Restore diary]'
	else
		case "${words[1]}" in
			edit)
				_arguments \
					'(- *)'{-h,--help}'[Display help message and exit]' \
					'(-p --past)'{-p,--past}'[Edit past diary]' && ret=0

				if [ -d "$datadir" ]; then
					past=false
					for i in $(seq ${#words[@]}); do
						if [ "${words[i]}" = -p ] || [ "${words[i]}" = --past ]; then
							past=true
							break
						fi
					done

					if [ "$past" = true ]; then
						_nikki_diaries "$datadir"
					else
						_values diary $(date '+%Y%m%d %m%d %d')
					fi
				fi
				;;
			ls)
				_arguments \
					'(- *)'{-h,--help}'[Display help message and exit]' && ret=0

				dates=($(find "$datadir" -type d -not \( -name nikki -or -name backup \) -print |
						 sed -e "s|^$datadir/||g" -e 's|/||g'))
				[ ${#dates[@]} -eq 0 ] && return

				[ -d "$datadir" ] && _values 'date' $dates
				;;
			read|rm)
				_arguments \
					'(- *)'{-h,--help}'[Display help message and exit]' && ret=0

				[ -d "$datadir" ] && _nikki_diaries "$datadir"
				;;
			tree|restore) _arguments '(- *)'{-h,--help}'[Display help message and exit]' && ret=0 ;;
		esac
	fi
}

compdef _nikki nikki
