#compdef nikki

_nikki_ls() {
	local i datadir
	local -a dates files

	[ $# -eq 1 ] || return
	datadir="$1"
	[ -d "$datadir/diaries" ] || return

	dates=($(find "$datadir/diaries" -type f -name '*.md' -print |
			 sed -e "s|^$datadir/diaries/||g" -e 's/\.md$//g' -e 's|/||g'))
	[ ${#dates[@]} -eq 0 ] && return

	i=$(tr ' ' '\n' <<<  "${dates[@]}")
	files+=($(sed 's/.\{2\}$//g' <<< "$i")) # %Y
	files+=($(sed 's/.\{4\}$//g' <<< "$i")) # %Y%m
	files+=($(grep -q "^$(date '+%Y%m')" <<< "$i" && date '+%m')) # Current month

	_values 'diary' $files
}

_nikki_diaries() {
	local i datadir
	local -a dates files

	[ $# -eq 1 ] || return
	datadir="$1"
	[ -d "$datadir/diaries" ] || return

	dates=($(find "$datadir/diaries" -type f -name '*.md' -print |
			 sed -e "s|^$datadir/diaries/||g" -e 's/\.md$//g' -e 's|/||g'))
	[ ${#dates[@]} -eq 0 ] && return

	i=$(tr ' ' '\n' <<<  "${dates[@]}")
	files+=("${dates[@]}")
	files+=($(sed -n "/^$(date '+%Y')/"'s/^.\{4\}//p' <<< "$i")) # Current month
	files+=($(sed -n "/^$(date '+%Y%m')/"'s/^.\{6\}//p' <<< "$i")) # Current date

	_values 'diary' $files
}

_nikki() {
	local context state state_descr line
	typeset -A opt_args

	local datadir past
	local -a dates

	datadir="${XDG_DATA_HOME:-$HOME/.local/share}/nikki"

	_arguments \
		'(- *)'{-h,--help}'[Display help message and exit]' \
		'(--pager)--pager[Set pager]:pager:(cat less bat glow)' \
		'(- *)--version[Display version information and exit]' \
		'1: :->cmd' \
		'*:: :->arg' && ret=0

	if [ "$state" = cmd ]; then
		_values 'subcommand' \
			'edit[Edit diaries]' \
			'ls[List diaries]' \
			'read[Read diaries]' \
			'rm[Remove diaries]' \
			'tree[List diaries in a tree-like format]' \
			'restore[Restore diaries]'
	else
		case "${words[1]}" in
			edit)
				_arguments \
					'(- *)'{-h,--help}'[Display help message and exit]' \
					'(-p --past)'{-p,--past}'[Edit past diaries]' && ret=0

				past=false
				for i in $(seq ${#words[@]}); do
					if [ "${words[i]}" = -p ] || [ "${words[i]}" = --past ]; then
						past=true
						break
					fi
				done

				if [ "$past" = true ]; then
					_nikki_diaries "$datadir"
				else
					_values diary $(date '+%Y%m%d %m%d %d')
				fi
				;;
			ls)
				_arguments \
					'(- *)'{-h,--help}'[Display help message and exit]' && ret=0

				_nikki_ls "$datadir"
				;;
			read|rm)
				_arguments \
					'(- *)'{-h,--help}'[Display help message and exit]' && ret=0

				_nikki_diaries "$datadir"
				;;
			tree|restore) _arguments '(- *)'{-h,--help}'[Display help message and exit]' && ret=0 ;;
		esac
	fi
}

compdef _nikki nikki
